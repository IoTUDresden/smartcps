apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "eu.vicci.ecosystem.standalone.controlcenter.android"
        minSdkVersion 16
        targetSdkVersion 25

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
	
	lintOptions {
		checkReleaseBuilds false
		// Or, if you prefer, you can continue to check for errors in release builds,
		// but continue the build even when errors are found:
		abortOnError false
     }
	
    // TODO clean this up. Better dependency organization needed
    packagingOptions {
        // excluding files, which causes duplicate exception and are not needed
        exclude 'META-INF/**'
        exclude 'maven_libs/**'
        exclude 'Readme.txt'
        exclude 'pom.xml'
        exclude 'about.html'
        exclude 'about.ini'
        exclude 'about.mappings'
        exclude 'about.properties'
        exclude 'modeling32.png'
        exclude 'plugin.properties'
        exclude 'plugin.xml'

        // some files included in more than one imported jar, so we just take the first
        pickFirst 'libs/dom4j-1.6.1.jar'
        pickFirst 'libs/gson-2.2.4.jar'
        pickFirst 'model/Ecore.ecore'
        pickFirst 'libs/java_websocket.jar'
        pickFirst 'schema/factory_override.exsd'
        pickFirst 'libs/hibernate-jpa-2.1-api-1.0.0.Final.jar'
        pickFirst 'schema/package_registry_implementation.exsd'
        pickFirst 'model/Ecore.genmodel'
        pickFirst 'hibernate.cfg.xml'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //DefaultHttpClient removed since api 23 - this will allow to use them
    useLibrary 'org.apache.http.legacy'
}

repositories {
    maven { url 'http://clojars.org/repo' }
}

dependencies {
    def withoutDeps = {
        exclude group: 'org.hibernate', module: 'hibernate-core'
        exclude group: 'xmlpull', module: 'xmlpull'
        exclude group: 'org.java-websocket', module: 'java-websocket'
        exclude group: 'org.java-websocket', module: 'Java-WebSocket'
    }

    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile "com.android.support:multidex:1.0.1"

    compile project(':AndroidNavigation')
    compile project(':melissa_library')
    compile project(':ProcessViewAndroid')
    compile project(':ControlcenterAndroid')
    compile project(':SystemApp')
	
	compile 'eu.vicci.proteus.bundles.model:eu.vicci.process.model:1.0.0-SNAPSHOT', withoutDeps
	compile 'eu.vicci.proteus.bundles.model:eu.vicci.process.core:1.0.0-SNAPSHOT', withoutDeps
	compile 'eu.vicci.proteus.bundles.model:eu.vicci.process.model.util:1.0.0-SNAPSHOT', withoutDeps

    compile 'eu.vicci.driver:eu.vicci.driver.robot:0.0.1-SNAPSHOT', withoutDeps
    compile 'eu.vicci.driver:eu.vicci.driver.turtlebot:0.0.1-SNAPSHOT', withoutDeps
    compile 'eu.vicci.driver:eu.vicci.driver.youbot:0.0.1-SNAPSHOT', withoutDeps
    compile 'eu.vicci.driver:eu.vicci.driver.nao:0.0.1-SNAPSHOT', withoutDeps

    compile 'org.java-websocket:java-websocket:1.3.1'
    compile 'ws.wamp.jawampa:jawampa-netty:0.4.2'

    // https://mvnrepository.com/artifact/org.eclipse.emf/
    compile 'org.eclipse.emf:org.eclipse.emf.ecore:2.8.3.v20130125-0546'
    compile 'org.eclipse.emf:org.eclipse.emf.common:2.8.0.v20130125-0546'
    compile 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.8.0-v20120911-0500'
}
